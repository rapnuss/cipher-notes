plugins {
    id 'com.android.application'
}

import java.util.Properties
import java.io.FileInputStream

def keystorePropertiesFile = file('keystore.properties')
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'com.ciphernotes.twa'
    compileSdk 35

    defaultConfig {
        applicationId 'com.ciphernotes.twa'
        minSdk 21
        targetSdk 35
        versionCode 6
        versionName '3.3'
    }

    signingConfigs {
        play {
            storeFile keystorePropertiesFile.exists() ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
        play {
            initWith release
            signingConfig signingConfigs.play
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.22')

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.webkit:webkit:1.12.0'
}
